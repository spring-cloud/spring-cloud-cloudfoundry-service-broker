/*
 * Copyright 2016-2019 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: "base"

buildscript {
	repositories {
		maven { url "https://repo.spring.io/plugins-release" }
	}
	dependencies {
		classpath("io.spring.nohttp:nohttp-gradle:0.0.3.RELEASE")
		classpath("io.spring.gradle:propdeps-plugin:0.0.10.RELEASE")
		classpath("io.spring.gradle:dependency-management-plugin:1.0.8.RELEASE")
		classpath("io.spring.gradle:spring-io-plugin:0.0.8.RELEASE")
		classpath("org.springframework.cloud:spring-cloud-contract-gradle-plugin:2.1.3.RELEASE")
		classpath("org.asciidoctor:asciidoctor-gradle-plugin:1.6.1")
	}
}

ext {
	springBootVersion = project.findProperty("springBootVersion") ?: "2.1.8.RELEASE"
	springFrameworkVersion = project.findProperty("springFrameworkVersion") ?: "5.1.9.RELEASE"
	// deprecated in favor of `springFrameworkVersion`
	if (project.hasProperty("springVersion")) {
		springFrameworkVersion = project.findProperty("springVersion")
	}
	reactorVersion = project.findProperty("reactorVersion") ?: "Californium-SR11"
	springCloudContractVersion = "2.1.3.RELEASE"
	checkstyleVersion = "8.21"

	javadocLinks = [
			"https://docs.oracle.com/javase/8/docs/api/",
			"https://docs.spring.io/spring/docs/${springFrameworkVersion}/javadoc-api/",
	] as String[]
}

//override managed Spring Boot versions
if (project.hasProperty("springFrameworkVersion") || project.hasProperty("springVersion")) {
	ext['spring-framework.version'] = ext.springFrameworkVersion
}
if (project.hasProperty("reactorVersion")) {
	ext['reactor-bom.version'] = ext.reactorVersion
}

description = "Spring Cloud Open Service Broker"

configure(allprojects) {
	group = "org.springframework.cloud"

	apply plugin: "java"
	apply plugin: "eclipse"
	apply plugin: "idea"
	apply plugin: "jacoco"
	apply plugin: "propdeps"
	apply plugin: "propdeps-maven"
	apply plugin: "propdeps-idea"
	apply plugin: "propdeps-eclipse"
	apply plugin: "io.spring.dependency-management"
	apply plugin: "io.spring.nohttp"

	apply from: "${rootProject.projectDir}/publish-maven.gradle"

	repositories {
		maven { url "https://repo.spring.io/libs-release" }
	}

	if (project.hasProperty("springFrameworkVersion") || project.hasProperty("springBootVersion")) {
		repositories {
			maven { url "https://repo.spring.io/libs-snapshot" }
		}
		buildscript {
			repositories {
				maven { url "https://repo.spring.io/libs-snapshot" }
			}
		}
	}

    // nohttp requires a valid checkstyle configuration
    checkstyle {
        configFile = file("${project.rootDir}/config/checkstyle-nohttp.xml")
        toolVersion = "${checkstyleVersion}"
    }
}

configure(allprojects - [project(":spring-cloud-open-service-broker-docs")]) {
	apply plugin: "checkstyle"
	apply plugin: "pmd"

	checkstyle {
		configFile = file("${project.rootDir}/config/checkstyle-header.xml")
		toolVersion = "${checkstyleVersion}"
	}
	checkstyleMain {
		source = "src/main/java"
	}
	checkstyleTest {
		source = "src/test/java"
	}

	pmd {
		toolVersion = "6.17.0"
	}
	pmdMain {
		ruleSets = []
		ruleSetFiles = files("${project.rootDir}/config/pmdRuleSet.xml")
		source = "src/main/java"
	}
	pmdTest {
		ruleSets = []
		ruleSetFiles = files("${project.rootDir}/config/pmdTestRuleSet.xml")
		source = "src/test/java"
	}
}

subprojects {
	task dependencyReport(type: DependencyReportTask)
}

configure(subprojects - [project(":spring-cloud-starter-open-service-broker")]) {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
	[compileJava, compileTestJava]*.options*.encoding = "UTF-8"

	[compileJava, compileTestJava]*.options*.compilerArgs = [
			"-Xlint:serial",
			"-Xlint:varargs",
			"-Xlint:cast",
			"-Xlint:classfile",
			"-Xlint:dep-ann",
			"-Xlint:divzero",
			"-Xlint:empty",
			"-Xlint:finally",
			"-Xlint:overrides",
			"-Xlint:path",
			"-Xlint:-processing",
			"-Xlint:static",
			"-Xlint:try",
			"-Xlint:fallthrough",
			"-Xlint:rawtypes",
			// Suppress deprecation warnings when building against latest integration releases
			(springBootVersion == "latest.integration" || springFrameworkVersion == "latest.integration")
					? "-Xlint:-deprecation" : "-Xlint:deprecation",
			"-Xlint:unchecked",
			"-Xlint:options",
			"-Werror"
	]

	javadoc {
		description = "Generates project-level javadoc for use in -javadoc jar"

		options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
		options.author = true
		options.header = project.name
		options.use = true
		options.links(javadocLinks)
		options.addStringOption('Xdoclint:none', '-quiet')
	}

	task sourcesJar(type: Jar, dependsOn: classes) {
		duplicatesStrategy = DuplicatesStrategy.EXCLUDE
		classifier = "sources"
		from sourceSets.main.allSource
	}

	task javadocJar(type: Jar) {
		classifier = "javadoc"
		from javadoc
	}

	task testsJar(type: Jar) {
		classifier = "tests"
		from sourceSets.test.output
	}

	artifacts {
		archives sourcesJar
		archives javadocJar
		archives testsJar
	}

	test {
		reports.junitXml.enabled = true
	}
}

configure(rootProject) {
	task sourcesJar(type: Jar, dependsOn: classes) {
		classifier = "sources"
		from sourceSets.main.allSource
	}

	task javadocJar(type: Jar, dependsOn: javadoc) {
		classifier = "javadoc"
		from javadoc
	}

	task apidocs(type: Javadoc) {
		group = "Documentation"
		description = "Generates aggregated Javadoc API documentation."
		title = "${rootProject.description} ${version} API"

		options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
		options.author = true
		options.header = rootProject.description
		options.links(javadocLinks)

		source subprojects.collect { project ->
			project.sourceSets.main.allJava
		}

		classpath = files(subprojects.collect { project ->
			project.sourceSets.main.compileClasspath
		})

		maxMemory = "1024m"
		destinationDir = new File(buildDir, "apidocs")
	}

	task docsZip(type: Zip, dependsOn: [':spring-cloud-open-service-broker-docs:asciidoctor']) {
		group = "Distribution"
		classifier = "docs"
		description = "Builds -${classifier} archive containing api and reference " +
				"for deployment."

		from(apidocs) {
			into "apidocs"
		}
		println project.tasks.findByPath(':spring-cloud-open-service-broker-docs:asciidoctor')
		from(project.tasks.findByPath(':spring-cloud-open-service-broker-docs:asciidoctor')) {
			into 'reference'
		}
	}

	artifacts {
		archives sourcesJar
		archives javadocJar
		archives docsZip

	}

	task dist(dependsOn: assemble) {
		group = "Distribution"
		description = "Builds -dist and -docs distribution archives."
	}
}

task codeCoverageReport(type: JacocoReport) {
	executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

	subprojects.each {
		sourceSets it.sourceSets.main
	}

	reports {
		xml.enabled true
		xml.destination new File("${buildDir}/reports/jacoco/report.xml")
		html.enabled false
		csv.enabled false
	}
}

codeCoverageReport.dependsOn {
	subprojects*.test
}
